{
  description = "CMOS Imaging Sensors: Digital Cameras for Astrophotography - Presentation";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/b86751bc4085f48661017fa226dee99fab6c651b";
    systems.url = "github:nix-systems/default";
  };

  outputs =
    {
      self,
      nixpkgs,
      systems,
      ...
    }:
    let
      forEachSystem =
        f: nixpkgs.lib.genAttrs (import systems) (system: f nixpkgs.legacyPackages.${system});

      runtimePackages =
        pkgs: with pkgs; [
          presenterm
          python3Packages.weasyprint
          yt-dlp
          ffmpeg
          imagemagick
        ];
    in
    {
      devShells = forEachSystem (pkgs: {
        default = pkgs.mkShell {
          buildInputs = runtimePackages pkgs;
        };
      });

      apps = forEachSystem (
        pkgs:
        let
          packages = runtimePackages pkgs;
        in
        {
          default = {
            type = "app";
            program = toString (
              pkgs.writeShellScript "run-presentation" ''
                export PATH="${pkgs.lib.makeBinPath packages}:$PATH"

                has_config=""
                for arg in "$@"; do
                  if [ "$arg" = "--config-file" ]; then
                    has_config=1
                    break
                  fi
                done

                if [ -z "$has_config" ]; then
                  set -- --config-file ${self}/config.yaml "$@"
                fi

                exec presenterm "$@" "$PWD/presentation.md"
              ''
            );
          };
        }
      );
    };
}
